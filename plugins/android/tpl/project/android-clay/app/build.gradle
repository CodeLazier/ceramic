import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    namespace "mycompany.myapp"

    compileSdk 34

    defaultConfig {
        applicationId "mycompany.myapp"
        minSdkVersion 21
        targetSdk 34
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a" /*, "x86", "x86_64"*/
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            jniDebuggable true
        }
        debugNativeRel {
            initWith debug
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/bind/java'
    }
}

task haxeBuildRelease(type: Exec) {
    workingDir '..'

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine './build-haxe.cmd', '--archs', 'armv7,arm64' //,x86,x86_64'
    }
    else {
        commandLine './build-haxe.sh', '--archs', 'armv7,arm64' //,x86,x86_64'
    }
}

task haxeBuildDebug(type: Exec) {
    workingDir '..'

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine './build-haxe.cmd', '--debug', '--archs', 'armv7,arm64' //,x86,x86_64'
    }
    else {
        commandLine './build-haxe.sh', '--debug', '--archs', 'armv7,arm64' //,x86,x86_64'
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'packageDebug' || task.name == 'generateDebugSources') {
        task.dependsOn haxeBuildDebug
    }
    else if (task.name == 'packageRelease' || task.name == 'generateReleaseSources') {
        task.dependsOn haxeBuildRelease
    }
    else if (task.name == 'packageDebugNativeRel' || task.name == 'generateDebugNativeRelSources') {
        task.dependsOn haxeBuildRelease
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
